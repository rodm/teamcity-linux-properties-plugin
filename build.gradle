
plugins {
    id 'com.github.rodm.teamcity-server' version '1.2.1'
    id 'com.github.rodm.teamcity-environments' version '1.2.1'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = 'com.github.rodm'
version = '1.0'

ext {
    teamcityVersion = project.findProperty('teamcity.api.version') ?: '2018.1'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_152'
}

dependencies {
    agent (project(path: ':agent', configuration: 'plugin'))
}

teamcity {
    version = "${teamcityVersion}"

    server {
        archiveName = "linux-properties-${rootProject.version}.zip"

        descriptor {
            name = 'linux-properties'
            displayName = 'Linux Properties'
            version = rootProject.version
            description = 'Provides additional properties to identify a Linux distribution.'
            vendorName = 'Rod MacKenzie'
            vendorUrl = 'https://github.com/rodm'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = "${rootDir}/data"

        'teamcity2018.1' {
            version = '2018.1.4'
            javaHome = file(java8Home)
        }
    }
}

bintray {
    user = findProperty('bintray.user')
    key = findProperty('bintray.key')

    filesSpec {
        from "${buildDir}/distributions"
        into 'linux-properties'
        include '*.zip'
    }

    dryRun = false
    publish = true
    override = false

    pkg {
        repo = 'teamcity-plugins-generic'
        name = 'linux-properties'
        desc = 'A TeamCity plugin that provides additional properties to identify a Linux distribution.'
        websiteUrl = 'https://github.com/rodm/teamcity-linux-properties-plugin'
        issueTrackerUrl = 'https://github.com/rodm/teamcity-linux-properties-plugin/issues'
        vcsUrl = 'https://github.com/rodm/teamcity-linux-properties-plugin.git'
        licenses = ['Apache-2.0']
        labels = ['teamcity', 'plugin', 'linux', 'properties']

        version {
            name = project.version
        }
    }
}
