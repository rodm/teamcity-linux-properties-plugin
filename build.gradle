
plugins {
    id 'com.github.rodm.teamcity-server' version '1.4'
    id 'com.github.rodm.teamcity-environments' version '1.4'
    id 'org.sonarqube' version '3.3'
}

group = 'com.github.rodm'
version = '1.0.1'

ext {
    teamcityVersion = project.findProperty('teamcity.api.version') ?: '2018.1'

    downloadsDir = project.findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = project.findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = project.findProperty('java8.home') ?: '/opt/jdk1.8.0_152'
}

dependencies {
    agent (project(path: ':agent', configuration: 'plugin'))
}

teamcity {
    version = "${teamcityVersion}"

    server {
        archiveName = "linux-properties-${rootProject.version}.zip"

        descriptor {
            name = 'linux-properties'
            displayName = 'Linux Properties'
            version = rootProject.version
            description = 'Provides additional properties to identify a Linux distribution.'
            vendorName = 'Rod MacKenzie'
            vendorUrl = 'https://github.com/rodm'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir
        baseDataDir = "${rootDir}/data"

        'teamcity2018.1' {
            version = '2018.1.4'
            javaHome = file(java8Home)
        }
    }
}
